WHITESPACE = _{ " " | "\n" | "\t" }

// Variables can contain ASCII alphabets, numerical digits and underscores
// but must not start with a digit
variable_start = _{ ASCII_ALPHA | "_" }
variable       = @{ variable_start ~ (variable_start | ASCII_DIGIT)* }

// An abstraction is: a "λ", followed by a list of (space separated) variables,
// followed by ".", followed by an expression
variable_list = _{ (variable ~ variable_list) | variable }
parameters    =  { variable_list }
abstraction   =  { "λ" ~ parameters ~ "." ~ expression }

// Expression can be just an atom, or a series of whitespace separated atoms
expression           = _{ (atom ~ expression) | atom }
bracketed_expression =  { "(" ~ expression ~ ")" }
atom                 = _{
    variable
  | bracketed_expression
  | abstraction
}

root_expression =  { expression }
full_string     = _{ SOI ~ root_expression ~ EOI }
